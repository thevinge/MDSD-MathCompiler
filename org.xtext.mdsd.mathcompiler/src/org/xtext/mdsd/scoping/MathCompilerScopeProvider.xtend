/*
 * generated by Xtext 2.20.0
 */
package org.xtext.mdsd.scoping

import org.eclipse.emf.ecore.EObject
import org.eclipse.emf.ecore.EReference
import org.eclipse.xtext.scoping.IScope
import org.eclipse.xtext.scoping.Scopes
import static extension java.util.Collections.singleton

import org.xtext.mdsd.mathCompiler.Variable
import org.xtext.mdsd.mathCompiler.Expression
import org.xtext.mdsd.mathCompiler.FunctionalBind
import org.xtext.mdsd.mathCompiler.MathExp
import org.xtext.mdsd.mathCompiler.MathCompilerPackage

/**
 * This class contains custom scoping description.
 * 
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#scoping
 * on how and when to use it.
 */
class MathCompilerScopeProvider extends AbstractMathCompilerScopeProvider {
	override getScope(EObject context, EReference reference) {
		if (reference == MathCompilerPackage.eINSTANCE.varReference_Variable) {
			return context.scope
		}
		return super.getScope(context, reference)
	}
	
	def private IScope getScope(EObject context) {
		val container = context.eContainer
		
		return switch (container) {
			
			MathExp: Scopes.scopeFor(
				container.eContents.filter(Variable))
					
			FunctionalBind case context instanceof Expression: Scopes.scopeFor(
				container.variable.singleton,
				container.scope
			)
			default: container.scope
			
		}
	}
}
